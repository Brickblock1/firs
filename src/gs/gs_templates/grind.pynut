/* Grind handles basic debugging and development tasks, for example
  - adding story pages for debugging information
  - using story buttons to invalidate or refresh state
*/

class GrindStoryBook {

    function Init() {
        // note that this creates a table in root
        // not designed to return a class instance
        // grind_story_book is a singleton, and allegedly a table with slots is more efficient for that case than a singleton class instance
        ::grind_story_book <- {};
        grind_story_book.show <- null;
        GrindStoryBook.CheckShowHide();
    }

    function CheckShowHide() {
        if (GSController.GetSetting("grind_show") != grind_story_book.show) {
            grind_story_book.show <- GSController.GetSetting("grind_show");
            if (grind_story_book.show) {
                GrindStoryBook.Show();
            } else {
                GrindStoryBook.Hide();
            }
        }
    }

    function Hide() {
        foreach (page_type in ["utility_pages", "industry_spec_pages"]) {
            foreach (grind_page in grind_story_book[page_type]) {
                GSStoryPage.Remove(grind_page.story_page);
            }
        }
    }

    function Show() {
        GrindStoryBook.AddUtilityPages();
        GrindStoryBook.AddIndustrySpecStoryPages();
    }

    function AddUtilityPages() {
        // note that this also automatically clears all current pages, that can be changed if needed, but trivially prevents duplication currently
        grind_story_book.utility_pages <- {};
        // note that currently there is just a single page provisioned
        grind_story_book.utility_pages.debug_page_1 <- GrindStoryPageUtility();
    }

    function AddIndustrySpecStoryPages() {
        // note that this also automatically clears all current pages, that can be changed if needed, but trivially prevents duplication currently
        grind_story_book.industry_spec_pages <- {};
        foreach (industry_id in firs.active_economy.GetIndustryIDsSortedByName()) {
            grind_story_book.industry_spec_pages[industry_id] <- GrindStoryPageIndustrySpec(industry_id);
        }
    }
}

class GrindStoryPage {
    /* base class for various types of Grind page */

    // keep a copy of the story page (for convenience)
    story_page = null;
}

class GrindStoryPageUtility extends GrindStoryPage {
    // provide a class in case we want to add more utility pages in future
    // but note that currently there is just a single page provisioned
    constructor() {
        this.story_page = GSStoryPage.New(GSCompany.COMPANY_INVALID, "Grind: LOLZ Dev Utils / Debugger");
        local run_init = GSStoryPage.NewElement(this.story_page, GSStoryPage.SPET_BUTTON_PUSH, 1, "Re-run Init()");
    }
}

class GrindStoryPageIndustrySpec extends GrindStoryPage {

    constructor(industry_id) {
        local industry_spec = firs.industries[industry_id];
        local industry_name = GSIndustryType.GetName(industry_spec.global_id);

        this.story_page = GSStoryPage.New(GSCompany.COMPANY_INVALID, "Grind: " + industry_name);
        // needs a dump info function that walks all the known properties ??
        // could foreach the industry_spec, but eh, doesn't seem needed right now
        local element_text = GSText(GSText.STR_GRIND_TEST, "industry_id: ", industry_id.tostring());
        local display_industry_id = GSStoryPage.NewElement(this.story_page, GSStoryPage.SPET_TEXT, 1, element_text);

        local props_to_display = ["grf_local_id", "global_id"];
        foreach (prop_name in props_to_display) {
            local element_text = GSText(GSText.STR_GRIND_TEST, prop_name, industry_spec[prop_name].tostring());
            local prop_name = GSStoryPage.NewElement(this.story_page, GSStoryPage.SPET_TEXT, 1, element_text);
        }
        // not sure we actually need map curation infor, it's trivially inspectable in src, but eh
        local map_curator = industry_spec.GetMapCurator();
        local map_curator_info = "null";
        if (map_curator != null) {
            map_curator_info = map_curator.tostring();
        }
        local element_text = GSText(GSText.STR_GRIND_TEST, "map_curator", map_curator_info);
        local display_map_curator_info = GSStoryPage.NewElement(this.story_page, GSStoryPage.SPET_TEXT, 1, element_text);
    }
}
